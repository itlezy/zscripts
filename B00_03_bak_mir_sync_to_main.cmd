@ECHO OFF

CD /D %~dp0

REM SET TO INVALID DRIVE
SET BAK_SRC_DRV=b

SET BAKMIR=bak_mir

(FOR %%C IN (%ALL_DRIVES_NOC%) DO (
	ECHO CHECKING %%C...
	IF EXIST %%C:\meta\bak_mir.main_source.dat (
		SET BAK_SRC_DRV=%%C
		ECHO FOUND MAIN BACKUP TARGET %%C BREAKING LOOP..
		GOTO :PROCEED
	)
))

:PROCEED
ECHO FOUND MAIN BACKUP TARGET %BAK_SRC_DRV%
PAUSE

IF NOT EXIST %BAK_SRC_DRV%:\%BAKMIR% (
	ECHO %BAK_SRC_DRV%:\%BAKMIR% NOT AVAILABLE, EXITING..
	PAUSE
	GOTO :EOF
)

CALL :DOMIRR "%USERPROFILE%\my\doc"        "%BAK_SRC_DRV%:\%BAKMIR%\backup-current-doc"
CALL :DOMIRR "%USERPROFILE%\my\doc"        "%USERPROFILE%\OneDrive\doc"
CALL :DOMIRR "%USERPROFILE%\my\gdrive\bak" "%BAK_SRC_DRV%:\%BAKMIR%\backup-current-rarpasswd"

GOTO :EOF

:DOMIRR
	SET SRCDIR=%~1
	SET TGTDIR=%~2

	ECHO MIRRORING FROM "%SRCDIR%" TO "%TGTDIR%"
	ROBOCOPY "%SRCDIR%" "%TGTDIR%" /MIR /J

GOTO :EOF
