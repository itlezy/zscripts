@ECHO OFF

CD /D %~dp0

REM SET TO INVALID DRIVE
SET BAK_SRC_DRV=b

SET BAKMIR=bak_mir

(FOR %%C IN (%ALL_DRIVES_NOC%) DO (
	ECHO CHECKING %%C...
	IF EXIST %%C:\meta\bak_mir.main_source.dat (
		SET BAK_SRC_DRV=%%C
		ECHO FOUND MAIN BACKUP SOURCE %%C BREAKING LOOP..
		GOTO:PROCEED
	)
))

:PROCEED
ECHO FOUND MAIN BACKUP SOURCE %BAK_SRC_DRV%
PAUSE

IF NOT EXIST %BAK_SRC_DRV%:\%BAKMIR% (
	ECHO %BAK_SRC_DRV%:\%BAKMIR% NOT AVAILABLE, EXITING..
	PAUSE
	EXIT
)

(FOR %%C IN (%ALL_DRIVES_NOC%) DO (
	CALL :DOMIRR %%C
))

EXIT

GOTO :EOF

:DOMIRR
	SET TGTDRV=%~1

	IF NOT EXIST %TGTDRV%:\meta\bak_mir.%BAKMIR%.dat (
		ECHO NOT A BACKUP MIRROR TARGET %TGTDRV% SKIPPING..
		GOTO :EOF
	)

	IF NOT EXIST %TGTDRV%:\%BAKMIR% (
		MKDIR %TGTDRV%:\%BAKMIR%
	)

	ECHO MIRRORING FROM %BAK_SRC_DRV% TO %TGTDRV%
	REM PAUSE

	REM BACKUP ONLY DIRS LISTED IN LST FILE
	SET MIR_LST=%TGTDRV%:\meta\bak_mir.%BAKMIR%.lst

	IF EXIST %MIR_LST% (

		FOR /F %%F IN (%MIR_LST%) DO (
			SET ZSRC=%BAK_SRC_DRV%:\%BAKMIR%\%%F
			SET ZTGT=%TGTDRV%:\%BAKMIR%\%%F

			TITLE MIRRORING FROM %BAK_SRC_DRV%:\%BAKMIR%\%%F TO %TGTDRV%:\%BAKMIR%\%%F
			IF EXIST "%BAK_SRC_DRV%:\%BAKMIR%\%%F" (
				ROBOCOPY /MIR "%BAK_SRC_DRV%:\%BAKMIR%\%%F" "%TGTDRV%:\%BAKMIR%\%%F"
			)
		)

	)

	REM BACKUP ALL BAK_MIR DIR
	IF NOT EXIST %MIR_LST% (

		IF EXIST %BAK_SRC_DRV%:\%BAKMIR% (
		IF EXIST %TGTDRV%:\%BAKMIR% (
			TITLE MIRRORING FROM %BAK_SRC_DRV% TO %TGTDRV%
			ROBOCOPY /MIR %BAK_SRC_DRV%:\%BAKMIR% %TGTDRV%:\%BAKMIR%
		))

	)

GOTO :EOF

EXIT
